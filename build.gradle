plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'antlr'
    id "kr.motd.sphinx" version "2.10.0"
}
group = 'org.smolang'
version = '0.2'
sourceCompatibility = '1.11'

repositories {
    mavenCentral()
}
test {
    useJUnitPlatform()
}
sourceSets {
    main {
        java.srcDirs += 'build/generated-src/main'
        kotlin.srcDirs += 'build/generated-src/main'
        antlr.srcDirs += 'src/main/antlr'
    }
}
dependencies {
    testImplementation("io.kotest:kotest-runner-junit5:5.2.3")
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation("org.apache.commons:commons-lang3:3.12.0")
    implementation('com.github.ajalt.clikt:clikt:3.4.2')
    implementation "org.antlr:antlr4:4.8"
    antlr "org.antlr:antlr4:4.8"
    implementation 'net.sourceforge.owlapi:org.semanticweb.hermit:1.4.5.519'
    implementation 'org.slf4j:slf4j-simple:1.7.25'
    implementation 'org.apache.jena:apache-jena-libs:3.16.0'
    implementation 'org.apache.jena:jena-core:3.16.0'
    implementation 'org.siani.javafmi:fmu-wrapper:2.26.3'
    implementation "com.influxdb:influxdb-client-kotlin:2.3.0"
    implementation 'com.sksamuel.hoplite:hoplite-core:1.4.1'
    implementation 'com.sksamuel.hoplite:hoplite-yaml:1.4.1'
    implementation("com.github.owlcs:ontapi:2.1.0")
    implementation 'org.jline:jline:3.21.0'
}

generateGrammarSource {
    outputDirectory = file("build/generated-src/main/no/uio/microobject/antlr")
    arguments += ["-visitor"]
}

compileKotlin {
    kotlinOptions.jvmTarget = project.sourceCompatibility
}
compileKotlin.dependsOn generateGrammarSource

jar {
    archiveBaseName='smol'
    manifest {
        attributes 'Main-Class': 'no.uio.microobject.main.MainKtKt'
    }
}
shadowJar {
    archiveBaseName='smol'
}
assemble.dependsOn shadowJar

sphinx {
    // Documentation at
    // https://trustin.github.io/sphinx-gradle-plugin/index.html
    sourceDirectory = "${projectDir}/manual/source"
    configDirectory = "${projectDir}/manual/source"
    outputDirectory = "${projectDir}/manual/build/html"
    builder = "html"
}
clean {
    delete files("${projectDir}/manual/build")
}
