//Standard Library
class <T> List(T content, List<T> next)
    Int length()
        if this.next = null then return 1;
        else Int n := this.next.length(); return n + 1;
        end
    end
    Int append(List<T> last)
        if this.next = null then
            this.next := last;
        else
            this.next.append(last);
        end
        return 0;
    end

    T get(Int i)
        T res := this.content;
        if i >= 1 then
            res := this.next.get(i-1);
        end
        return res;
    end

    Boolean contains(T element)
        if this.content = element then
            return True;
        else
            if this.next = null then
                return False;
            else
                Boolean res := this.next.contains(element);
                return res;
            end
        end
    end
end


//Wrapper for simulators: each simulator can have multiple ports
abstract class <T2> OutPort()
    abstract T2 get()
end
abstract class <T3> InPort()
    abstract Boolean write(T3 t)
end

//Co-sim connection
abstract class Connection()
    abstract Int propagate()
end

class ImplConnection extends Connection (OutPort<Double> from, InPort<Double> to)
    override Int propagate()
        Double val := this.from.get();
        this.to.write(val);
        return 0;
    end
end

//Jacobi master algorithm: propagate through all connections, advance time for all FMUs at once
class CoSim(List<Connection> list, List<DE[;]> sims, Double stepSize)
    Int round()
        Int i := 0;
        if this.list = null then return i; end
        Int length := this.list.length();
        while i < length do
            Connection c := this.list.get(i);
            c.propagate();
            i := i+1;
        end
        i := 0;
        if this.sims = null then return i; end
        length := this.sims.length();
        while i < length do
            DE[;] de := this.sims.get(i);
            tick(de, this.stepSize);
            i := i + 1;
        end
        return i;
    end

    Int execute(Int steps)
        Int i := 1;
        while( i <= steps ) do
            this.round();
            i := i+1;
        end
        return i;
    end
end

//example
class PreyOutPort extends OutPort<Double>(DE[Double y; Double x] prey)
    override Double get() return this.prey.x; end
end
class PredatorOutPort extends OutPort<Double>(DE[Double x; Double y] predator)
    override Double get() return this.predator.y; end
end
class PreyInPort extends InPort<Double>(DE[Double y; Double x] prey)
    override Boolean write(Double t) this.prey.y := t; return True; end
end
class PredatorInPort extends InPort<Double>(DE[Double x; Double y] predator)
    override Boolean write(Double t) print(t); this.predator.x := t; return True; end
end

main
    DE[Double y; Double x] prey := simulate("examples/SimulationDemo/Prey.fmu", y := 10);
    DE[Double x; Double y] predator := simulate("examples/SimulationDemo/Predator.fmu", x := 10);
    PreyOutPort preyOut := new PreyOutPort(prey);
    PredatorOutPort predOut := new PredatorOutPort(predator);
    PreyInPort preyIn := new PreyInPort(prey);
    PredatorInPort predIn := new PredatorInPort(predator);
    Connection c1 := new ImplConnection(preyOut, predIn);
    Connection c2 := new ImplConnection(predOut, preyIn);
    List<DE[;]> fmus := new List<DE[;]>(prey, null);
    fmus := new List<DE[;]>(predator, fmus);
    List<Connection> cons := new List<Connection>(c1, null);
    cons := new List<Connection>(c2, cons);
    CoSim sim := new CoSim(cons, fmus, 0.2);
    sim.execute(200);

    List<InPort<Double>> ll := access("SELECT ?obj WHERE {?con a prog:ImplConnection. ?con prog:to ?obj}");
    while ll <> null do
        InPort<Double> inp := ll.content;
        ll := ll.next;
        print(inp);
    end
end